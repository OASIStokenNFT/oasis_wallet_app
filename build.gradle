apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

apply plugin: "org.jmailen.kotlinter"

Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

android {
    def STRING = "String"
    def INFURA_PROJECT_ID = "INFURA_PROJECT_ID"
    def INFURA_PROJECT_SECRET = "INFURA_PROJECT_SECRET"
    def ETHERESCAN_KEY = "ETHERSCAN_KEY"

    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.fridaytech.dex"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 19
        versionName "1.1.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }

        release {
            storeFile file(properties.getProperty("keystore", "path_to_kestore_file"))
            storePassword properties.getProperty("keystore_pass", "your_keystore_pass")
            keyAlias properties.getProperty("key_alias", "your_key_alias")
            keyPassword properties.getProperty("key_pass", "your_key_pass")
        }
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            buildConfigField STRING, INFURA_PROJECT_ID, "\"0c3f9e6a005b40c58235da423f58b198\""
            buildConfigField STRING, INFURA_PROJECT_SECRET, "\"57b6615fb10b4749a54b29c2894a00df\""
            buildConfigField STRING, ETHERESCAN_KEY, "\"NHURUUGCB2HDQFS26CKTRT51K4PHBY7234\""
        }

        release {
            minifyEnabled false
            debuggable false
            useProguard false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField STRING, INFURA_PROJECT_ID, properties.getProperty("infura_project_id", "your_project_id")
            buildConfigField STRING, INFURA_PROJECT_SECRET, properties.getProperty("infura_project_secret", "your_infura_project_secret")
            buildConfigField STRING, ETHERESCAN_KEY, properties.getProperty("etherscan_key", "your_etherscan_key")
        }
    }

    flavorDimensions "network"
    productFlavors {
        devTestnet {
            buildConfigField "boolean", "testMode", "true"
            applicationIdSuffix ".dev.testnet"
        }
        devMainnet {
            buildConfigField "boolean", "testMode", "false"
            applicationIdSuffix ".dev"
        }
        productionMainnet {
            buildConfigField "boolean", "testMode", "false"
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinter {
        ignoreFailures = false
        reporters = ['checkstyle', 'plain']
        experimentalRules = false
        fileBatchSize = 30
        disabledRules = ["no-wildcard-imports"]
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.browser:browser:1.2.0-beta01'
    implementation 'androidx.biometric:biometric:1.0.0'

    implementation 'com.google.android.play:core:1.6.4'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // Retrofit
    def retrofitVersion = '2.6.2'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    // Room
    def roomVersion = '2.2.2'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-rxjava2:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.2.11'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    // ZrxKit
    implementation 'com.github.5daytech:zrx-kit-android:0.3.1'

    // EthereumKit
    implementation 'com.github.horizontalsystems:ethereum-kit-android:055850e'

    // HdWalletKit
    implementation 'com.github.horizontalsystems:hd-wallet-kit-android:a3666d8'

    // FeeRateKit
    implementation 'com.github.horizontalsystems:blockchain-fee-rate-kit-android:15df939'

    // Rates
    implementation 'com.github.5daytech:xrates-kit-android:d913bcef31'

    // Qr
    implementation 'com.journeyapps:zxing-android-embedded:3.6.0'

    // Chart
    implementation 'com.github.PhilJay:MPAndroidChart:3.0.3'

    // Bottom Navigation
    implementation 'com.aurelhubert:ahbottomnavigation:2.3.4'

    // SwipeBack
    implementation 'me.imid.swipebacklayout.lib:library:1.1.0'
}

androidExtensions {
    experimental = true
}
